* Clear eveything in the result viewer and log before running code;
ODS HTML CLOSE; ODS HTML;
dm 'log;clear;output;clear;';

* ------------ Also store output as PDF file ----------------;
options nodate topmargin=.5in bottommargin=.5in leftmargin=.5in rightmargin=.5in center; run;

/* Sample code to create own style*/
proc template;
	define style mystyle;
		parent=styles.htmlblue;
		style usertext from usertext / foreground=blue;
	end;
run;

ods escapechar='\';
ods pdf file='H:\statcomp\Project\SASproject_groupX.pdf' style=mystyle startpage=no;

title1 "SAS Homework: Wildfire analysis in the state of North Carolina \nHerlinde Wynendaele - Laurens Van Paemel - Steven Vandeleene - Bart Moens ";
footnote 'Page \{thispage}';
title2 "\n\nIntroduction";
ods pdf text="\S={just=c}Every single year human lives, ecosystems and properties are at risk due to wildfires in the state of North-Carolina (NC).
 Wildfires harm both air and water quality while efforts to fight wildfires are expensive and put emergency personel in harms way.
 A better understanding of the dynamics behind destructive wildfires can aid prevention efforts. This report analyses the causes,
 timings and sizes of wildfires accross different counties in NC. The data is obtained from the United States Forest Service (USFS)
 and spans accross 1992 to 2015.\n\n
 The analyses in this report have been performed on a dataset containing the following categorical
 variables: the county the fire occured in, it's fips code, the year and month the fire was
 discovered in, the official cause with a matching cause code and the fire size binned into seven categories. The continuous variables 
 studied are the discovery date of the fire, the fire size and the coordinates of the fire. The only missing values in the dataset are
 for the variables county and the fips code. The county variable also contained several erroneousness entrees, all of which were correctable. Below the
 first 5 rows of the dataset are depicted as an example.\n\n";
run;
title;

ods pdf exclude all;

/****************** 1. Importing the data *********************************/
* Set library;
libname Project "H:\statcomp\Project";

* Import data;
proc import OUT= project.FireNC DATAFILE= "H:\statcomp\Project\fires_NC.csv" dbms=csv replace;
	getnames=yes;
	datarow=2;
	guessingrows=10000;
run; 

ods HTML select Contents.DataSet.Variables;
* Descriptive portion of the data; 
title1 "Descriptive portion Wild fire NC region";
proc contents data=project.FireNC;
run;
Title1;

/****************** 5-6. Define data formats *******************************/
proc format;
  value asmonth
    1='January'  2='February'  3='March'      4='April'     5='May'        6='June'
    7='July'     8='August'    9='September'  10='October'  11='November'  12='December';

  value $cause_und
    'Missing/Undefined'='Unknown cause'		'Debris Burnin'='Debris Burning';

  value $countytxt
    1='Alamance'   3='Alexander'  5='Alleghany'   7='Anson'     9='Ashe'       13='Beaufort' 15='Bertie'
    17='Bladen'    25='Cabarrus'  29='Camden'     33='Caswell'  35='Catawba'   37='Chatham'  41='Chowan'
    45='Cleveland' 47='Columbus'  51='Cumberland' 59='Davie'    61='Duplin'    63='Durham'   65='Edgecombe'
    67='Forsyth'   69='Franklin'  71='Gaston'     73='Gates'    77='Granville' 79='Greene'   81='Guilford'
    83='Halifax'   85='Harnett'   91='Hertford'   93='Hoke'     95='Hyde'      97='Iredell'  101='Johnston'
    103='Jones'    105='Lee'      107='Lenoir'    109='Lincoln' 111='McDowell' 113='Macon'   115='Madison'
    117='Martin'   121='Mitchell' 125='Moore'     127='Nash'    133='Onslow'   135='Orange'  137='Pamlico'
    141='Pender'   145='Person'   147='Pitt'      149='Polk'    151='Randolph' 159='Rowan'   163='Sampson'
    167='Stanly'   169='Stokes'   171='Surry'     173='Swain'   177='Tyrrell'  179='Union'   181='Vance'
    183='Wake'     185='Warren'   189='Watauga'   191='Wayne'   195='Wilson'   197='Yadkin'  199='Yancey'
    119='Mecklenburg'  123='Montgomery'   129='New Hanover'  131='Northampton'   139='Pasquotank'   143='Perquimans'
    157='Rockingham'   161='Rutherford'   175='Transylvania' 187='Washington'    21, 021='Buncombe' 11, 011='Avery'
    23, 023='Burke'    27, 027='Caldwell' 31, 031='Carteret' 39, 039='Cherokee'  43, 043='Clay'     49, 049='Craven'
    57, 057='Davidson' 75, 075='Graham'   87, 087='Haywood'  89, 089='Henderson' 99, 099='Jackson'  19, 'Brunswick Co'='Brunswick'
    53, 'Currituck Co'='Currituck'  55, 'Federal Land', 'Dare Bomb Ra'='Dare'  153, 'Richmond Cou'='Richmond'
    155, 'Robeson Coun'='Robeson'   165, 'Scotland Cou'='Scotland'             193, 'Wilkes Count'='Wilkes';

  value size_class
	0<-0.25 = 'A'	0.25<-<10 = 'B' 10-<100 = 'C' 100-<300 = 'D' 300-<1000 = 'E' 1000-<5000 = 'F' 5000-high = 'G';
run;

/*********** Label the data and apply formatting ***********************/
data work.subset1; 
	set project.FireNC; 
	label 	FIRE_YEAR = "Calendar year in which the fire was discovered or confirmed to exist."
			DISCOVERY_DATE = "Date on which the fire was discovered or confirmed to exist."
			STAT_CAUSE_CODE = "Code for the (statistical) cause of the fire."
			STAT_CAUSE_DESCR = "Cause of the fire."
			FIRE_SIZE = "Estimate of acres within the final perimeter of the fire."
			FIRE_SIZE_CLASS = "Code for fire size based on the number of acres within the final fire perimeter expenditures (A=greater than 0 but less than or equal to 0.25 acres, B=0.26-9.9 acres, C=10.0-99.9 acres, D=100-299 acres, E=300 to 999 acres, F=1000 to 4999 acres, and G=5000+ acres)."
			LATITUDE = "Latitude (NAD83) for point location of the fire (decimal degrees)."
			LONGITUDE = "Longitude (NAD83) for point location of the fire (decimal degrees)."
			STATE = "Two-letter alphabetic code for the state in which the fire burned (or originated), based on the nominal designation in the fire report."
			COUNTY = "County, or equivalent, in which the fire burned (or originated), based on nominal designation in the fire report."
			FIPS_CODE = "Three-digit code from the Federal Information Process Standards (FIPS) publication 6-4 for representation of counties and equivalent entities.";
	MONTH = month(datepart(DISCOVERY_DATE));
	format	COUNTY $countytxt.
			STAT_CAUSE_DESCR $cause_und.
			MONTH asmonth3.;
run; 

* Descriptive portion of the data; 
ods HTML select Contents.DataSet.Variables;
title1 "Descriptive portion Wild fire NC region including label";
proc contents data=work.subset1;
run;
Title1;

/****************** 2. Subset of the data **********************************/
* Keeping select variables;
DATA Project.fnc;
    set work.subset1;
    keep state county fips_code discovery_date fire_year fire_size latitude longitude stat_cause_code stat_cause_descr fire_size_class month;
RUN;

ods PDF select Print.Print;
* Display the first five rows of the new table;
title1 "Top 5 Observations of the data set used for further analysis";
proc print data = project.fnc (obs=5) LABEL;
  label	month='Month' fire_year='Year' fire_size='Fire size' latitude='Latitude' longitude='Longitude' STAT_CAUSE_CODE='Cause code'
		STATE='State' COUNTY='County' STAT_CAUSE_DESCR='Cause' fips_code='Fips code' FIRE_SIZE_CLASS='Fire size class' DISCOVERY_DATE='Discovery date';
  format latitude comma10.2 longitude comma10.2 DISCOVERY_DATE datetime7.;
run;
title1;

/************* 3. Levels of categorical variables **************************/
%let categorical = state county fips_code fire_year stat_cause_code stat_cause_descr;
ods select Freq.NLevels;
* Amount of levels and missing values for each variable with a character format;
title1 "Categorical Variables: the amount of levels and missing values";
proc freq data = Project.fnc nlevels;
	tables &categorical;
run;
title1;
ods PDF text="\S={just=c}The table displays the number of levels for each categorical variable.
 The values for the state variable are all the same, namely 'NC' as a reference to the
 state of North-Carolina. Both the county and fips_code variables have 100 different
 levels and missing values. The data spans accross 24 years and contains thirteen
 different categories for causes.\n\n";

/* 4. Inconsistencies, missing values for continuous variables and extreme observations */
%let continuous = discovery_date fire_size latitude longitude;
* Display missing data pattern;
ods pdf exclude all;
ods HTML select MI.MissPattern;
title1 "Missing data pattern numeric variables";
proc mi data=Project.fnc nimpute=0;
	var _numeric_;
run;
title1;

* For all numeric variables display 3 extreme observations;
title1 "Detect inconsistencies in data";
proc univariate data=Project.fnc nextrobs=3;
run;
Title1;

* Continuous variable distribution analysis;
title1 "Continuous variable distribution analysis";
proc univariate data=Project.fnc;
   var fips_code fire_year fire_size latitude longitude ;
   histogram fips_code fire_year fire_size latitude longitude /normal kernel;
   inset n mean std/position = ne;
run;
Title1;

ods PDF text="\S={just=c}There do not appear to be any inconsistencies in any continuous variables. However, the size of the wildfires is skewed
 to the right, with a few unusually large wildfires. The most extreme observation being a wildfire which, according to the data, burned
 45294 acres of land. There's no reason to assume these extreme values are errors as they appear to be realistic.\n\n";

/************** 7. Months with the highest number of fires *****************/
ods graphics on / width=6in height=3.5in;
ods PDF select Freq.Table1.OneWayFreqPlots.FreqPlot;
title1 "Number of fires per month";
proc freq data=Project.fnc;
	Tables MONTH / plots=freqplot(scale=freq) out = freqout;
run;
title1;
ods PDF text="\S={just=c}During late winter and early spring wildfires occure more often while summer is the calmest period.\n\n";

/**************** 8. Barchart: number of fires by cause ********************/
ods PDF select SGPlot.SGPlot;
title1 "Number of fires per cause";
proc sgplot data=Project.fnc;
	vbar STAT_CAUSE_DESCR / CATEGORYORDER=RESPDESC;
	xaxis label = "Cause of fire";
	Yaxis label = "frequency";
run;
Title;
ods PDF text="\S={just=c}By far the most common cause of wildfires is debris burning, which accounts for more than one third of all wildfires.
 A distant second is Arson. The third most common official cause is equipment use, which has barely been documented more often compared
 to wildfires caused by children. However, the large amount of unknown causes leaves some room for ambiguity. The category of causes
 'miscellaneous', by assumption, consists of a multitude of causes that cannot be assigned to any other category and thus to hold data
 for numerous less common causes. The category of wildfires with an unknown cause is not taken into account. \n\n";

/**************** 9. Examining the fire size by county *********************/
ods PDF exclude Means.Summary;
*Calculate N Mean LCLM UCLM by county.;
proc means data = Project.fnc N Mean LCLM UCLM;
	var FIRE_SIZE;
	class COUNTY;
	output out=fnc_all_means N=n_obs mean=average LCLM=lower_CI UCLM=upper_CI;
run;

ods pdf exclude Print.Print;
*Sort by average descending.;
proc sort data=fnc_all_means out=Project.fnc_means;
	by descending Average;
run;
proc print data = Project.fnc_means;
run;

ods PDF select Print.Print;
*Print the first observation having the highest average fire_size;
title1 "Observation with highest average fire size";
proc print data = Project.fnc_means (obs = 1);
run;
title1;
ods PDF text="\S={just=c}The county with historically the largest average fire size is Pender, where wildfires have on average burned 77.76 acres each.\n\n";

/********** 10. Top 10 historically largest average fire size **************/
* Use the result of the means procedure;
Proc Sql noprint;
	Select quote(county) into :top10counties separated by ' ' from Project.fnc_means(obs=10);
Quit;

* Average fire size by county;
ods PDF select SGPlot.SGPlot;
title 'Top 10 counties with the largest average fire size';
proc sgplot data = Project.fnc;
label county = 'County'
   fire_size = 'Average fire size';
vbar county / response=fire_size stat=mean CATEGORYORDER=RESPDESC;
xaxis values=(&top10counties);
run;
title;
ods PDF text="\S={just=c}The bar chart displays the 10 counties where historically the average fire
 size has been the largest. The county of Pender clearly towers above the others.\n\n";

/****************** 11. Fire size by month *********************************/
ods graphics on / width=6in height=8in;
ods PDF select SGPanel.SGPanel startpage=yes;
proc sgpanel data=Project.fnc;
  title "Causes of wildfires by month";
  where STAT_CAUSE_DESCR <> "Missing/Undefined";
  panelby STAT_CAUSE_DESCR / uniscale=row proportional spacing=1 novarname
		HEADERATTRS=(color = 'white' size=10pt Weight=Bold)
		HEADERBACKCOLOR='grey'
		COLHEADERPOS=TOP
		COLUMNS=2
		ROWS=6;
  vbar MONTH / datalabel;
run;
ods PDF text="\S={just=c}With the exception of lightning and fireworks, most causes follow the same trend. Namely a spike in late winter and early spring,
 while summer has the least occurences of wildfires. Fireworks and lightning go against the trend as they more often cause wildfires during summer.\n\n";

/********* 12. Number of fires over time for the 3 main causes *************/
ods graphics on / width=6in height=3.5in;
ods PDF select SGPlot.SGPlot;
proc sgplot data = Project.fnc; 
	where STAT_CAUSE_DESCR in ("Debris Burning", "Arson", "Equipment Use") ; 
	title "Main causes of wildfires over time";
	vline FIRE_YEAR / group = STAT_CAUSE_DESCR MARKERS;
run;
title;
ods PDF text="\S={just=c}The amount of wildfires due to debris burning and arson has been on the decline while the amount
 of wildfires caused by equipment use seem to be quite steady over time.\n\n";

/* 13. Top 3 causes: number of fires, total fire size, average fire size and max fire size */
ods PDF select Tabulate.Report.Table startpage=no;
title 'Top 3 causes of fire size: descriptive statistics' ;
proc tabulate data = Project.fnc format = 8.3  S=[foreground=black just=c cellwidth=120];
	where STAT_CAUSE_DESCR in ('Arson', 'Debris Burning', 'Equipment Use');
	class STAT_CAUSE_DESCR/order=freq  style={font_style=italic} ;
	var FIRE_SIZE/style={font_style=italic};
	table STAT_CAUSE_DESCR, FIRE_SIZE*(N sum mean max) / style={width=5in};
	label STAT_CAUSE_DESCR='Top 3 causes of fire' FIRE_SIZE='Fire Size: Estimate of acres within the final perimeter of the fire' ; 
	keylabel N='Number of fires' Sum='Total size' Mean='Average size' Max='Maximum size'; 
run;
title;
ods PDF text="\S={just=c}Eventhough debris burning causes more wildfires, those wildfires are on average a lot more contained compared
 to those caused by arson and equipment use. Wildfires caused by arson have torched on average 30 % more acres of land while debris
 burning accounts for more than double the amount of wildfires.\n\n";

/**************** 14. Log transformed fire size ****************************/
ods PDF exclude all;
* log transformed fire size and only the three main causes;
DATA Project.fnclog;
	SET Project.fnc;
	logtransform= log(FIRE_SIZE);
	where STAT_CAUSE_DESCR in ('Arson', 'Debris Burning', 'Equipment Use');
RUN;

* Is the log transformed fire size normally distributed for the 3 causes? graphically & formal testing;
proc univariate data=Project.fnclog noprint;
	class STAT_CAUSE_DESCR;
	var logtransform;
	histogram logtransform / nrow=3 normal odstitle="Histogram for the top 3 Fire causes";
	qqplot logtransform/ nrow=3 normal odstitle="QQ-plot for the top 3 Fire causes"; 
	inset n mean std / position=ne;
run;
* Conclusion: log transformed data is not normally distributed - P-value is < 0.05 (we may assume data is not normally distributed);

*create rtf;
ods rtf file='combine_graphandtable.rtf' startpage=no nogtitle nogfootnote;
ods rtf select histogram qqplot GoodnessOfFit;
ods graphics/ height=800px width=700px;
title "Is the log transformed fire size normally distributed for each of the top 3 causes?";
proc univariate data=Project.fnclog noprint;
	class STAT_CAUSE_DESCR;
	var logtransform;
	histogram logtransform / nrow=3 normal odstitle="Histogram for the top 3 Fire causes";
	qqplot logtransform/ nrow=3 normal odstitle="QQ-plot for the top 3 Fire causes"; 
	inset n mean std / position=ne;
run;
ods text = "We conclude that the log transformed fire size distributions are not normally distributed";
title;
ods rtf close;
ods PDF text="\S={just=c}-------------We conclude that the log transformed fire size distributions are not normally distributed---------------\n\n";

/*** 15. Top 3 causes: significant difference in the fire size in 2015? ****/
ods PDF select GLM.LSMEANS.STAT_CAUSE_DESCR.FIRE_SIZE.DiffPlot;
ods output OverallANOVA=outputtest;
ods graphics on / width=4in height=4in;
proc glm data=Project.fnc plots=all;
	where fire_year = 2015 AND STAT_CAUSE_DESCR in ('Arson', 'Debris Burning', 'Equipment Use');
    class stat_cause_descr;
    model fire_size=stat_cause_descr;
    lsmeans stat_cause_descr / pdiff=all adjust=tukey;
    lsmeans stat_cause_descr / pdiff=control('Average/Typical') adjust=dunnett;
run;
quit;

/**************** 16. Macro-value from p-value *****************************/
*From the results generated in question 15, take p-value is selected from the first row.;
data _NULL_;
	set Outputtest;
	if _n_ = 1 then call symputx('PValue', ProbF);
	*Output the p-value to the log;
	put ProbF pvalue6.4;
run;
ods PDF text="\S={just=c}The p-values are larger than 0.05 thus there's no significant difference between the fire sizes caused by any of the top three causes in 2015.\n\n";
title2 "Conclusion\n\n";
ods PDF text="\S={just=c}Conclusion....................\n\n";
title2;
ods pdf close;
